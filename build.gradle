plugins {
	id("com.github.johnrengelman.shadow") version "7.1.2"
	id("io.micronaut.application") version "3.5.1"
	id("gg.jte.gradle") version "2.1.2"
	id("org.ethelred.kv2.java-conventions")
	id "co.uzzu.dotenv.gradle" version "2.0.0"
}

version = "0.1"
group = "org.ethelred"

repositories {
	mavenCentral()
	mavenLocal()
}

micronaut {
	runtime("netty")
	testRuntime("junit5")
	processing {
		incremental(true)
		annotations("org.ethelred.kv2.*")
	}
}

dependencies {
	annotationProcessor("io.micronaut:micronaut-http-validation")
	annotationProcessor("io.micronaut.data:micronaut-data-processor")
	annotationProcessor("io.micronaut.security:micronaut-security-annotations")
	implementation("io.micronaut:micronaut-http-client")
	implementation("io.micronaut:micronaut-runtime")
	implementation("io.micronaut:micronaut-management")
	implementation("io.micronaut.data:micronaut-data-jdbc")
	implementation("io.micronaut.liquibase:micronaut-liquibase")
	implementation("io.micronaut.security:micronaut-security")
	implementation("io.micronaut.security:micronaut-security-jwt")
	implementation("io.micronaut.security:micronaut-security-oauth2")
	implementation("io.micronaut.sql:micronaut-jdbc-hikari")
	implementation("io.micronaut.views:micronaut-views-jte")
	implementation("gg.jte:jte:2.1.2")
	implementation("io.micronaut.reactor:micronaut-reactor")
	implementation("io.micronaut.reactor:micronaut-reactor-http-client")
	implementation("javax.annotation:javax.annotation-api")
	runtimeOnly("ch.qos.logback:logback-classic")
	runtimeOnly("mysql:mysql-connector-java")
	runtimeOnly("org.testcontainers:jdbc")
	runtimeOnly("org.testcontainers:mysql")

	testImplementation("org.testcontainers:junit-jupiter")
	testImplementation("org.testcontainers:mysql")
	testImplementation("org.testcontainers:testcontainers")
	implementation("io.micronaut:micronaut-validation")
	implementation 'com.github.ksuid:ksuid:1.1.1'
}


application {
	mainClass.set("org.ethelred.kv2.Application")
}

tasks.named('run') {
	environment(env.allVariables)
	dependsOn startMysqlContainer
	finalizedBy stopMysqlContainer
}

jte {
	generate()
	generateNativeImageResources = true
	binaryStaticContent = true
}

ext.containerName = 'kv2_dev_mysql'
import com.bmuschko.gradle.docker.tasks.container.*
import com.github.dockerjava.api.exception.ConflictException

task createMysqlContainer(type: DockerCreateContainer) {
	targetImageId 'mysql:8'
	containerName = project.containerName
	hostConfig.portBindings = ['3306:3306']
	envVars = [MYSQL_ROOT_PASSWORD:'super-secret', MYSQL_DATABASE:'db', MYSQL_USER:'kv2', MYSQL_PASSWORD:'12345']
	onError {
		if (it instanceof ConflictException) {
			logger.warn("create container failed, assuming it already exists")
		} else {
			throw it
		}
	}
}

task startMysqlContainer(type: DockerStartContainer) {
	dependsOn createMysqlContainer
	targetContainerId project.containerName
	onError {
		logger.warn("start container failed, assuming it is running", it)
	}
}

task stopMysqlContainer(type: DockerStopContainer) {
	targetContainerId project.containerName
	onError {
		logger.warn("stop container failed, assuming it did not exist", it)
	}
}